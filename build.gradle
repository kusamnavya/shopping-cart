buildscript {
	ext {
		springBootVersion = '1.5.14.RELEASE'

		nexusUrl = System.env.NEXUS_HOST
		nexusPort = System.env.NEXUS_PORT
		nexusUser = System.env.NEXUS_USER
		nexusPassword = System.env.NEXUS_PASSWORD
	}
	repositories {
		maven {
			url "http://${nexusUrl}:${nexusPort}/repository/maven-public/"
		}
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply from: 'gradle/spring-boot.gradle'

jar {
	baseName = 'shopping-cart'
	version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8

repositories {
	maven {
		url "http://${nexusUrl}:${nexusPort}/repository/maven-public/"
	}
	mavenCentral()
}

test{
	systemProperties = System.properties
}

sourceSets{
	test{
		java {
			srcDirs = ['src/test/java']
		}    // no source dirs for the java compiler
		groovy {
			srcDirs = ['src/test/groovy']
		}  // compile everything in src/ with groovy
	}
}


configurations {
	deployerJars
}

dependencies {
	deployerJars "org.apache.maven.wagon:wagon-http:2.2+"

	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-jdbc'){
		exclude module: 'tomcat-jdbc'
	}
	runtime('org.springframework.boot:spring-boot-devtools')
	
	compile group: 'com.google.guava', name: 'guava', version: '23.0'
	compile("org.hibernate:hibernate-java8:5.0.0.Final")
	compile("com.zaxxer:HikariCP:2.6.0")

	runtime('com.h2database:h2')
	runtime('mysql:mysql-connector-java:5.1.40')

	compileOnly("org.projectlombok:lombok:${lombokVersion}")
	
	testCompile("org.codehaus.groovy:groovy-all:2.4.8")
	
	testCompile("org.spockframework:spock-core:1.1-groovy-2.4-rc-3"){
		exclude module: "groovy-all"
	}
	
	testCompile("org.spockframework:spock-spring:1.1-groovy-2.4-rc-3"){
		exclude module: "groovy-all"
		exclude module: "spock-core"
	}
	testCompile group: 'org.assertj', name: 'assertj-core-java8', version: '1.0.0m1'
	testCompile('org.springframework.boot:spring-boot-starter-test')
}


uploadArchives {
	repositories {
		mavenDeployer {
			configuration = configurations.deployerJars
		
			repository(url: "http://${nexusUrl}:${nexusPort}/repository/maven-releases/") {
				authentication(userName: nexusUser, password: nexusPassword)
			}
			snapshotRepository(url: "http://${nexusUrl}:${nexusPort}/repository/maven-snapshots/") {
				authentication(userName: nexusUser, password: nexusPassword)
			}

			pom {

				version = "${jar.version}"
				artifactId = "${jar.baseName}"
				groupId = 'com.ecommerce.shopping'
			}
		}
	}
}

